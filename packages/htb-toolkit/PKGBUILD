pkgname=htb-toolkit
pkgver=43.3724a30
pkgrel=1
pkgdesc='Play Hack The Box machines directly on your system.'
arch=('x86_64' 'aarch64')
url='https://github.com/D3vil0p3r/htb-toolkit'
license=('GPL')
depends=('coreutils' 'gzip' 'imagemagick' 'libsecret' 'openssl' 'openvpn')
makedepends=('git' 'cargo')
source=("git+https://github.com/D3vil0p3r/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname
  
  cargo clean
  cargo build --release --locked
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/share/$pkgname"
  cp -a icons "$pkgdir/usr/share/$pkgname/"

  install -Dm 755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
