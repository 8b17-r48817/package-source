pkgname=htb-toolkit
pkgver=29.a44a6e8
pkgrel=1
pkgdesc='Play Hack The Box machines directly on your system.'
arch=('x86_64' 'aarch64')
url='https://github.com/D3vil0p3r/htb-toolkit'
license=('GPL')
depends=('coreutils' 'gnome-keyring' 'gzip' 'imagemagick' 'openvpn')
makedepends=('git' 'cargo')
source=("git+https://github.com/D3vil0p3r/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  cargo build --release --locked
}

package() {
  cd $pkgname

  install -Dm 755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
