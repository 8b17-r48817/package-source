pkgname=athena-parrotctfs-theme
pkgver=205.3864d851aa
pkgrel=1
pkgdesc="Parrot CTFs theme for Athena."
arch=('any')
license=("GPL3")
depends=('fish' 'layan-gtk-theme-git' 'shell-color-scripts' 'starship')
makedepends=('git')
conflicts=('athena-fish')
source=("git+https://github.com/rtlewis88/rtl88-Themes#branch=material-black-COLORS"
        "git+https://github.com/TeddyBearKilla/Afterglow-Cursors-Recolored"
        "git+https://github.com/parrotassassin15/ConfigFiles"
        "PCTFs.png"
        "$pkgname.tar.gz")
sha512sums=('SKIP'
            'SKIP'
            'SKIP'
            '70bd0d55c90fcc0355528551596ec5a439d7c79c87b4c2aa8260d67e19e69283bceeb99cfc4a0d8912a6036bcd112186b275ec96a63e5a5cf16936474bf7c6c8'
            '11520dbb9fe0f94e7e6755218a5117d0e9ab35f16013494dc1dfb6c16279a23bfd16fa423b0a8da518dd33b5cdc372a8a64236697375cf20e0c99617ddbe39a5')

pkgver() {
  cd rtl88-Themes

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  install -dm 755 "$pkgdir/usr/share/icons/hicolor/256x256"
  install -dm 755 "$pkgdir/usr/share/icons/Afterglow-Recolored-Purple"
  install -dm 755 "$pkgdir/usr/share/backgrounds/default"
  install -dm 755 "$pkgdir/etc/skel/.config"

  cp -a rtl88-Themes/Material-Black-Cherry-Suru "$pkgdir/usr/share/icons/"
  cp -a Afterglow-Cursors-Recolored/colors/Original/Purple/dist/* "$pkgdir/usr/share/icons/Afterglow-Recolored-Purple/"
  install -Dm 644 "PCTFs.png" "$pkgdir/usr/share/backgrounds/default/"

  cp -a "$srcdir/icons/apps" "$pkgdir/usr/share/icons/hicolor/256x256/"
  cp -a "$srcdir/icons/categories" "$pkgdir/usr/share/icons/hicolor/256x256/"

  install -Dm 644 "ConfigFiles/custom-prompt/starship/starship.toml" "$pkgdir/etc/skel/.config/"
  cp -a "ConfigFiles/custom-prompt/fish" "$pkgdir/etc/skel/.config/"
}
