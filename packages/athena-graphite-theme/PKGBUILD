pkgname=athena-graphite-theme
pkgver=2023.05.17.r8.gb2e9ce2
pkgrel=1
pkgdesc="Graphite Dark theme with Tela Circle Black Dark icons for Athena."
arch=('any')
license=("GPL3")
depends=('gtk-update-icon-cache')
makedepends=('git' 'gnome-shell' 'sassc')
source=("git+https://github.com/vinceliuice/Graphite-gtk-theme.git"
        "git+https://github.com/vinceliuice/Tela-circle-icon-theme.git"
        "https://github.com/ful1e5/Bibata_Cursor/releases/latest/download/Bibata-Modern-Ice.tar.gz"
        "$pkgname"
        "arch-ascii.png"
        "$pkgname.tar.gz")
sha512sums=('SKIP'
            'SKIP'
            'bb11cc73a15b26ad98818a70e22547fb1be0984e0f92250e1c5a8f42a537552a1193d53136d91290ca6fb2e52b6cee217c9c66551b7308195d2da8ee37dfe554'
            '2857c98db677cdb97af7222fa260890054bc777f406fe744d4ba5067330b701af58a2494007e5a47f3732c480bd10c2119b3867a8955f5bada239cf2a5681fa5'
            '180debc64a86b577ca3f465157ccf853f87d74ba9f101559ddda74a32169910d7b0fef829fa7672bfb306c58131ab27300154557e36bc402c833f097244dd18e'
            '3640aaec96819e655479d731a74cb3b69aae1514d64372e68a5dafa701be207719a5a6b116088d56a89badc7ff5b9b6f2c8865d7a308592a5bf39a92af444a7f')

pkgver() {
  cd Graphite-gtk-theme

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/themes"
  install -dm 755 "$pkgdir/usr/share/icons/hicolor/256x256"
  install -dm 755 "$pkgdir/usr/share/backgrounds/default"

  cp -a Bibata-Modern-Ice "$pkgdir/usr/share/icons/"
  cp -a "$srcdir/icons/apps" "$pkgdir/usr/share/icons/hicolor/256x256/"
  cp -a "$srcdir/icons/categories" "$pkgdir/usr/share/icons/hicolor/256x256/"
  install -Dm 755 "$pkgname" "$pkgdir/usr/bin/"
  install -Dm 644 "arch-ascii.png" "$pkgdir/usr/share/backgrounds/default/"

  cd Graphite-gtk-theme

  ./install.sh -n Graphite -c dark -d $pkgdir/usr/share/themes
  ./install.sh -n Graphite-Rimless -c dark --tweaks rimless -d $pkgdir/usr/share/themes

  rm -rf $pkgdir/usr/share/themes/Graphite-Dark-hdpi $pkgdir/usr/share/themes/Graphite-Dark-xhdpi $pkgdir/usr/share/themes/Graphite-Rimless-Dark-hdpi $pkgdir/usr/share/themes/Graphite-Rimless-Dark-xhdpi


  cd ../Tela-circle-icon-theme

  ./install.sh black -d "$pkgdir/usr/share/icons" -c

}
