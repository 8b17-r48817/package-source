pkgname=athena-graphite-theme
pkgver=2023.05.17.r4.gac8cfc4
pkgrel=1
pkgdesc="Graphite Dark theme with Tela Circle Black Dark icons for Athena"
arch=('any')
license=("GPL3")
makedepends=('git' 'sassc')
source=("git+https://github.com/vinceliuice/Graphite-gtk-theme.git"
        "git+https://github.com/vinceliuice/Tela-circle-icon-theme.git"
        "https://github.com/ful1e5/Bibata_Cursor/releases/latest/download/Bibata-Modern-Ice.tar.gz"
        "arch-ascii.png"
        "$pkgname.tar.gz")
sha512sums=('SKIP'
            'SKIP'
            'bb11cc73a15b26ad98818a70e22547fb1be0984e0f92250e1c5a8f42a537552a1193d53136d91290ca6fb2e52b6cee217c9c66551b7308195d2da8ee37dfe554'
            '180debc64a86b577ca3f465157ccf853f87d74ba9f101559ddda74a32169910d7b0fef829fa7672bfb306c58131ab27300154557e36bc402c833f097244dd18e'
            'b542968d6473b1f03b4506ebc7227525a0dde0e29aec800abfc32ee3414fe690bcf0ab6657ddd4817017531254dd59f28e9d334dd6c8d849261e566ed27ae566')

pkgver() {
  cd Graphite-gtk-theme

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {

  install -dm 755 "$pkgdir/usr/share/themes"
  install -dm 755 "$pkgdir/usr/share/icons/hicolor/256x256"
  install -dm 755 "$pkgdir/usr/share/backgrounds/default"

  cp -a Bibata-Modern-Ice "$pkgdir/usr/share/icons/"
  cp -a "$srcdir/icons/apps" "$pkgdir/usr/share/icons/hicolor/256x256/"
  cp -a "$srcdir/icons/categories" "$pkgdir/usr/share/icons/hicolor/256x256/"
  install -Dm 644 "arch-ascii.png" "$pkgdir/usr/share/backgrounds/default/"

  cd Graphite-gtk-theme

  ./install.sh -n Graphite -l -c dark -d $pkgdir/usr/share/themes
  ./install.sh -n Graphite-Rimless -l -c dark --tweaks rimless -d $pkgdir/usr/share/themes

  rm -rf $pkgdir/usr/share/themes/Graphite-Dark-hdpi $pkgdir/usr/share/themes/Graphite-Dark-xhdpi $pkgdir/usr/share/themes/Graphite-Rimless-Dark-hdpi $pkgdir/usr/share/themes/Graphite-Rimless-Dark-xhdpi


  cd ../Tela-circle-icon-theme

  ./install.sh black -d "$pkgdir/usr/share/icons" -c

}
