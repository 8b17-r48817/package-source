pkgname=athena-gruvbox-theme
pkgver=57.c0b7fb50
pkgrel=1
pkgdesc="Gruvbox theme with Material Black Mango Suru icons for Athena."
arch=('any')
license=("GPL3")
makedepends=('git')
source=("git+https://github.com/Fausto-Korpsvart/Gruvbox-GTK-Theme.git"
        "git+https://github.com/rtlewis88/rtl88-Themes#branch=material-black-COLORS"
        "https://github.com/ful1e5/fuchsia-cursor/releases/latest/download/Fuchsia-Pop.tar.gz"
        "$pkgname"
        "cyborg_gruv.png"
        "$pkgname.tar.gz")
sha512sums=('SKIP'
            'SKIP'
            '6007a5db701981997719a38ae99181b6269a58332f245b31b6756a5fcad6b9f8bb991c5820c16b16419005cf5631ffa3410c9c9068b16d09e69042734aa99eb0'
            '7ec89209878205c8d26ee22c64f7c87853d47e2acba40f59d4c001d1553ac4a48e376e7c52e95748798bf3c88b48be61c12026c783e031458625ba295583d2b7'
            'bbd4a82c1dda3dc26271e36c2ff7c362d513e720696fa9912e7d025f049337f551a6f1e1969cb087303c2dd02344ac27523e1a6662d16daca9c616bbd7c80445'
            '66e4999417b321d60898878d92b8ebf33f5c90994bc75740846bf73c13f861b69781ff49b659a5cac4b44d3fe165a0111290fdbc934e9cf940ac7e581f6284aa')

pkgver() {
  cd Gruvbox-GTK-Theme

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/themes"
  install -dm 755 "$pkgdir/usr/share/icons/hicolor/256x256"
  install -dm 755 "$pkgdir/usr/share/backgrounds/default"

  cp -a Gruvbox-GTK-Theme/themes/Gruvbox-Dark-B "$pkgdir/usr/share/themes/"
  cp -a Gruvbox-GTK-Theme/themes/Gruvbox-Dark-BL "$pkgdir/usr/share/themes/"
  cp -a rtl88-Themes/Material-Black-Mango-Suru "$pkgdir/usr/share/icons/"
  cp -a Fuchsia-Pop "$pkgdir/usr/share/icons/"
  install -Dm 755 "$pkgname" "$pkgdir/usr/bin/"
  install -Dm 644 "cyborg_gruv.png" "$pkgdir/usr/share/backgrounds/default/"

  cp -a "$srcdir/icons/apps" "$pkgdir/usr/share/icons/hicolor/256x256/"
  cp -a "$srcdir/icons/categories" "$pkgdir/usr/share/icons/hicolor/256x256/"

}
